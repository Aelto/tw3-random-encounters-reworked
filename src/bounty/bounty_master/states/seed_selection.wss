state SeedSelection in RER_BountyMasterManager {
  event OnEnterState(previous_state_name: name) {
    super.OnEnterState(previous_state_name);

    NLOG("RER_BountyMasterManager - SeedSelection");

    this.SeedSelection_main();
  }

  entry function SeedSelection_main() {
    // the player decided not to have the seed selector window show up. In this
    // case, we directly notify the bounty manager we want a bounty with the
    // seed 0.
    // The seed 0 is a special case, with this seed everything is completely
    // random and none of the values depend on the seed. Two bounties with
    // the seed 0 are not guaranteed to be the same unlike other seeds.
    if (RER_menuBool('RERoptionalFeatures', 'RERignoreSeedSelectorWindow')) {
      parent.bountySeedSelected(0);
    }
    else {
      this.openHaggleWindow();
    }
  }

  private function openHaggleWindow() {
    var haggle_module_dialog: RER_BountyModuleDialog;

    haggle_module_dialog = new RER_BountyModuleDialog in parent;
    haggle_module_dialog.openSeedSelectorWindow(parent);
  }
}
