
state Ending in RandomEncountersReworkedHuntingGroundEntity {
  event OnEnterState(previous_state_name: name) {
    super.OnEnterState(previous_state_name);

    NLOG("RandomEncountersReworkedHuntingGroundEntity - State Ending");

    this.Ending_main();
  }

  entry function Ending_main() {
    var distance_from_player: float;

    distance_from_player = VecDistanceSquared2D(
      thePlayer.GetWorldPosition(),
      parent.GetWorldPosition()
    );

    // The distance check could cause issues if the player lures monsters outside
    // the radius and kill them there.
    if (parent.is_bounty && distance_from_player < 150 * 150) {
      if (parent.bounty_group_index < 0) {
        parent.bounty_manager.notifyMainGroupKilled();
      }
      else {
        parent.bounty_manager.notifySideGroupKilled(parent.bounty_group_index);
      }
    }

    if (VecDistanceSquared(thePlayer.GetWorldPosition(), parent.bait_entity.GetWorldPosition()) < 50 * 50) {
      RER_tryRefillRandomContainer(parent.master);
      RER_emitEncounterKilled(EncounterType_HUNTINGGROUND);
    }
    else {
      RER_emitEncounterRecycled(EncounterType_HUNTINGGROUND);
    }

    if (!parent.manual_destruction) {
      parent.clean();
    }
  }
}
