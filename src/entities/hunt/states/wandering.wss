
state Wandering in RandomEncountersReworkedHuntEntity {
  event OnEnterState(previous_state_name: name) {
    super.OnEnterState(previous_state_name);

    NLOG("RandomEncountersReworkedHuntEntity - State Wandering");
    this.Wandering_main();
  }

  entry function Wandering_main() {
    this.resetEntitiesActions();
    this.makeEntitiesMoveTowardsBait();
    this.resetEntitiesActions();

    parent.GotoState('Combat');
  }

  latent function makeEntitiesMoveTowardsBait() {
    var distance_from_player: float;
    var distance_from_bait: float;
    var current_entity: CEntity;
    var current_heading: float;
    var is_player_busy: bool;
    var i: int;

    do {
      if (SUH_areAllEntitiesDead(parent.entities)) {
        NLOG("HuntEntity - wandering state, all entities dead");

        parent.GotoState('Ending');

        break;
      }

      for (i = parent.entities.Size() - 1; i >= 0; i -= 1) {
        current_entity = parent.entities[i];
        is_player_busy = isPlayerInScene();

        distance_from_player = VecDistance(
          current_entity.GetWorldPosition(),
          thePlayer.GetWorldPosition()
        );

        if (distance_from_player > parent.entity_settings.kill_threshold_distance) {
          NLOG("killing entity - threshold distance reached: " + parent.entity_settings.kill_threshold_distance);

          parent.killEntity(current_entity);

          continue;
        }

        if (distance_from_player < 15
        || distance_from_player < 20 && (current_entity as CActor).HasAttitudeTowards(thePlayer)) {
          // Leave the function, it will automatically enter in the Combat state
          // and the creatures will attack the player.
          return;
        }

        distance_from_bait = VecDistanceSquared(
          current_entity.GetWorldPosition(),
          parent.bait_entity.GetWorldPosition()
        );

        if (distance_from_bait < 5 * 5) {
          if (is_player_busy) {
            teleportBaitEntityOnMonsters();
          }
          else {
            this.teleportBaitEntity();
          }
        }

        if (is_player_busy) {
          (parent.entities[i] as CActor).SetTemporaryAttitudeGroup(
            'q104_avallach_friendly_to_all',
            AGP_Default
          );
        }
        else if (!(current_entity as CActor).IsMoving()) {
          (parent.entities[i] as CActor).SetTemporaryAttitudeGroup(
            'monsters',
            AGP_Default
          );

          (current_entity as CActor).ActionCancelAll();

          (current_entity as CNewNPC)
            .NoticeActor(parent.bait_entity as CActor);
        }

        #ifdef ENABLE_TRAIL_MAKER {
          parent.trail_maker.addTrackHere(
            current_entity.GetWorldPosition(),
            current_entity.GetWorldRotation()
          );
        };
      }

      // To keep the bait near the player at all time.
      if (parent.bait_moves_towards_player) {
        if (RER_moveCreaturesAwayIfPlayerIsInCutscene(parent.entities, 30)) {
          teleportBaitEntityOnMonsters();
        }
        else {
          this.teleportBaitEntity();
        }
      }

      Sleep(0.5);
    } while (true);
  }

  private function teleportBaitEntity() {
    var new_bait_position: Vector;
    var new_bait_rotation: EulerAngles;

    // NDEBUG("towards player " + parent.bait_moves_towards_player);

    if (parent.bait_moves_towards_player) {
      new_bait_position = thePlayer.GetWorldPosition();
      new_bait_rotation = parent.bait_entity.GetWorldRotation();
    }
    else {
      new_bait_position = parent.getRandomEntity().GetWorldPosition() + VecConeRand(parent.bait_entity.GetHeading(), 90, 10, 20);
      new_bait_rotation = parent.bait_entity.GetWorldRotation();
      new_bait_rotation.Yaw += RandRange(-20,20);
    }
    
    parent.bait_entity.TeleportWithRotation(
      new_bait_position,
      new_bait_rotation
    );
  }

  // This function always teleports the bait on the monsters.
  // Often used to pause the hunt during cutscenes or when the player is busy.
  latent function teleportBaitEntityOnMonsters() {
    var new_bait_position: Vector;
    var new_bait_rotation: EulerAngles;
    var random_entity: CEntity;

    new_bait_position = parent.getRandomEntity().GetWorldPosition();
    new_bait_rotation = parent.bait_entity.GetWorldRotation();
    
    parent.bait_entity.TeleportWithRotation(
      new_bait_position,
      new_bait_rotation
    );
  }

  latent function resetEntitiesActions() {
    var i: int;
    var current_entity: CEntity;
    
    for (i = parent.entities.Size() - 1; i >= 0; i -= 1) {
      current_entity = parent.entities[i];

      (current_entity as CActor).ActionCancelAll();

      (current_entity as CActor)
          .GetMovingAgentComponent()
          .ResetMoveRequests();

      (current_entity as CActor)
          .GetMovingAgentComponent()
          .SetGameplayMoveDirection(0.0);
    }
  }
}
