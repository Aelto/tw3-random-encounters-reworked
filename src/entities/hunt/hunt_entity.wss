
struct HuntEntitySettings {
  var kill_threshold_distance: float;
  var allow_trophy_pickup_scene: bool;
}

statemachine class RandomEncountersReworkedHuntEntity extends CEntity {
  var master: CRandomEncounters;
  
  var entities: array<CEntity>;

  var bestiary_entry: RER_BestiaryEntry;

  var entity_settings: HuntEntitySettings;

  var bait_entity: CEntity;

  var trail_maker: RER_TrailMaker;

  var bait_moves_towards_player: bool;

  var oneliner: RER_OnelinerEntity;


  public function startEncounter(master: CRandomEncounters, entities: array<CEntity>, bestiary_entry: RER_BestiaryEntry, optional bait_moves_towards_player: bool) {
    this.master = master;
    this.entities = entities;
    this.bestiary_entry = bestiary_entry;
    this.bait_moves_towards_player = bait_moves_towards_player;
    this.loadSettings(master);
    this.addOneliner();
    this.GotoState('Loading');
  }

  private function addOneliner() {
    if (this.bait_moves_towards_player && !RER_menuBool('RERoptionalFeatures', 'RERonelinersAmbush')) {
      return;
    }

    if (!this.bait_moves_towards_player && !RER_menuBool('RERoptionalFeatures', 'RERonelinersHunt')) {
      return;
    }

    if (this.entities.Size() > 0) {
      var first: CEntity = this.entities[0];

      this.oneliner = RER_onelinerEntity(
        (new SUOL_TagBuilder in thePlayer)
          .tag("font")
          .attr("color", "#9e9e9e")
          .text(this.bestiary_entry.toLocalizedName()),
        first
      );
      
      this.oneliner.setRenderDistance(50);
    }
  }

  private function loadSettings(master: CRandomEncounters) {
    this.entity_settings.kill_threshold_distance = master.settings.kill_threshold_distance;
    this.entity_settings.allow_trophy_pickup_scene = master.settings.trophy_pickup_scene;
  }

  public latent function clean() {
    var i: int;

    NLOG("RandomEncountersReworkedHuntEntity destroyed");

    if (this.oneliner) {
      this.oneliner.unregister();
    }

    for (i = 0; i < this.entities.Size(); i += 1) {
      this.killEntity(this.entities[i]);
    }

    #ifdef ENABLE_TRAIL_MAKER {
      trail_maker.clean();
    };

    this.Destroy();
  }

  ///////////////////////////////////////////////////
  // below are helper functions used in the states //
  ///////////////////////////////////////////////////

  public function killEntity(entity: CEntity): bool {
    (entity as CActor).Kill('RandomEncountersReworked_Entity', true);

    return this.entities.Remove(entity);
  }

  public function getRandomEntity(): CEntity {
    var entity: CEntity;

    entity = this.entities[RandRange(this.entities.Size())];

    return entity;
  }
}
