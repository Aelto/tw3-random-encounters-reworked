
function getGroupPositions(initial_position: Vector, count: int, density: float): array<Vector> {
  var s, r, x, y: float;
  var pos_fin: Vector;
  var output_positions: array<Vector>;
  
  var i: int;
  var sign: int;

  // Const values used in the loop.
  pos_fin.Z = initial_position.Z;
  s = count / density; // maintain a constant density of `density` unit per m2
  r = SqrtF(s/Pi());

  for (i = 0; i < count; i += 1) {
    x = RandF() * r;        // add random value within range to X
    y = RandF() * (r - x);  // add random value to Y so that the point is within the disk

    if(RandRange(2)) {       // randomly select the sign for misplacement
      sign = 1;
    }
    else {
      sign = -1;
    }
      
    pos_fin.X = initial_position.X + sign * x;  //final X pos
    
    if(RandRange(2)) {        // randomly select the sign for misplacement
      sign = 1;
    }
    else {
      sign = -1;
    }
      
    pos_fin.Y = initial_position.Y + sign * y;  //final Y pos

    // Return false means it could not find ground position.
    // In this case, take the default position.
    // If return true, then pos_fin is updated with the correct position.
    if (!getGroundPosition(pos_fin)) {
      pos_fin = initial_position;
    }

    output_positions.PushBack(pos_fin);
  }

  return output_positions;
}
