
// When the player is hurt (not full health) necrophage ambushes can appear around him.
class RER_ListenerBloodNecrophages extends RER_EventsListener {
  var time_before_other_spawn: float;
  default time_before_other_spawn = 0;

  var trigger_chance: float;

  private var already_spawned_this_combat: bool;


  public latent function loadSettings() {
    var inGameConfigWrapper: CInGameConfigWrapper;

    inGameConfigWrapper = theGame.GetInGameConfigWrapper();

    this.trigger_chance = StringToFloat(
      inGameConfigWrapper
      .GetVarValue('RERevents', 'eventBloodNecrophages')
    );

    // The event is only active if its chances to trigger are greater than 0.
    this.active = this.trigger_chance > 0;
  }

  public latent function onInterval(was_spawn_already_triggered: bool, master: CRandomEncounters, delta: float, chance_scale: float): bool {
    var type: CreatureType;
    var is_in_combat: bool;
    var health_missing_perc: float;

    is_in_combat = thePlayer.IsInCombat();

    // To avoid triggering more than one event per fight.
    if (is_in_combat && (was_spawn_already_triggered || this.already_spawned_this_combat)) {
      this.already_spawned_this_combat = true;

      return false;
    }

    // To avoid triggering this event too frequently.
    if (this.time_before_other_spawn > 0) {
      time_before_other_spawn -= delta;

      return false;
    }
    
    this.already_spawned_this_combat = false;

    // This will be used to scale the % chances based on the missing health.
    // The lower the health the higher the chances.
    health_missing_perc = 1 - thePlayer.GetHealthPercents();

    if (RandRangeF(100) < this.trigger_chance * chance_scale * health_missing_perc) {
      if (shouldAbortCreatureSpawn(master.settings, master.rExtra, master.bestiary)) {
        NLOG("RER_ListenerBloodNecrophages - cancelled");

        return false;
      }

      type = this.getRandomNecrophageType(master);
      createRandomCreatureAmbush(master, type);

      // So that we don't spawn an ambush too frequently.
      this.time_before_other_spawn += master.events_manager.internal_cooldown;
      
      NLOG("RER_ListenerBloodNecrophages - spawn triggered type = " + type);

      return true;
    }

    return false;
  }

  private latent function getRandomNecrophageType(master: CRandomEncounters): CreatureType {
    var spawn_roller: SpawnRoller;
    var creatures_preferences: RER_CreaturePreferences;
    var i: int;
    var can_spawn_creature: bool;
    var manager : CWitcherJournalManager;
    var roll: SpawnRoller_Roll;

    spawn_roller = new SpawnRoller in this;
    spawn_roller.fill_arrays();

    creatures_preferences = new RER_CreaturePreferences in this;
    creatures_preferences
      .setIsNight(theGame.envMgr.IsNight())
      .setExternalFactorsCoefficient(master.settings.external_factors_coefficient)
      .setIsNearWater(master.rExtra.IsPlayerNearWater())
      .setIsInForest(master.rExtra.IsPlayerInForest())
      .setIsInSwamp(master.rExtra.IsPlayerInSwamp())
      .setIsInCity(master.rExtra.isPlayerInSettlement() || master.rExtra.getCustomZone(thePlayer.GetWorldPosition()) == REZ_CITY)
      .setCurrentRegion(AreaTypeToName(theGame.GetCommonMapManager().GetCurrentArea()));

    creatures_preferences
      .reset();

    master.bestiary.entries[CreatureGHOUL]
      .setCreaturePreferences(creatures_preferences, EncounterType_DEFAULT)
      .fillSpawnRoller(spawn_roller);

    master.bestiary.entries[CreatureALGHOUL]
      .setCreaturePreferences(creatures_preferences, EncounterType_DEFAULT)
      .fillSpawnRoller(spawn_roller);

    master.bestiary.entries[CreatureDROWNER]
      .setCreaturePreferences(creatures_preferences, EncounterType_DEFAULT)
      .fillSpawnRoller(spawn_roller);

    master.bestiary.entries[CreatureDROWNERDLC]
      .setCreaturePreferences(creatures_preferences, EncounterType_DEFAULT)
      .fillSpawnRoller(spawn_roller);

    master.bestiary.entries[CreatureROTFIEND]
      .setCreaturePreferences(creatures_preferences, EncounterType_DEFAULT)
      .fillSpawnRoller(spawn_roller);

    master.bestiary.entries[CreatureWEREWOLF]
      .setCreaturePreferences(creatures_preferences, EncounterType_DEFAULT)
      .fillSpawnRoller(spawn_roller);

    master.bestiary.entries[CreatureEKIMMARA]
      .setCreaturePreferences(creatures_preferences, EncounterType_DEFAULT)
      .fillSpawnRoller(spawn_roller);

    master.bestiary.entries[CreatureKATAKAN]
      .setCreaturePreferences(creatures_preferences, EncounterType_DEFAULT)
      .fillSpawnRoller(spawn_roller);

    master.bestiary.entries[CreatureHAG]
      .setCreaturePreferences(creatures_preferences, EncounterType_DEFAULT)
      .fillSpawnRoller(spawn_roller);

    master.bestiary.entries[CreatureFOGLET]
      .setCreaturePreferences(creatures_preferences, EncounterType_DEFAULT)
      .fillSpawnRoller(spawn_roller);

    master.bestiary.entries[CreatureBRUXA]
      .setCreaturePreferences(creatures_preferences, EncounterType_DEFAULT)
      .fillSpawnRoller(spawn_roller);

    master.bestiary.entries[CreatureFLEDER]
      .setCreaturePreferences(creatures_preferences, EncounterType_DEFAULT)
      .fillSpawnRoller(spawn_roller);

    master.bestiary.entries[CreatureGARKAIN]
      .setCreaturePreferences(creatures_preferences, EncounterType_DEFAULT)
      .fillSpawnRoller(spawn_roller);

    master.bestiary.entries[CreatureDETLAFF]
      .setCreaturePreferences(creatures_preferences, EncounterType_DEFAULT)
      .fillSpawnRoller(spawn_roller);

    #ifdef ENABLE_BESTIARY_FEATURE {
      // When the option "Only known bestiary creatures" is ON
      // we remove every unknown creatures from the spawning pool.
      if (master.settings.only_known_bestiary_creatures) {
        manager = theGame.GetJournalManager();

        for (i = 0; i < CreatureMAX; i += 1) {
          can_spawn_creature = bestiaryCanSpawnEnemyTemplateList(master.bestiary.entries[i].template_list, manager);
          
          if (!can_spawn_creature) {
            spawn_roller.setCreatureCounter(i, 0);
          }
        }
      }
    };

    roll = spawn_roller.rollCreatures(master.ecosystem_manager);
    return roll.roll;
  }
}