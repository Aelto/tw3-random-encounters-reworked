
statemachine class RER_PresetManager {
  public var done: bool;
  public var master: CRandomEncounters;

  public function displayPresetAppliedPopup(preset_name: string) {
    NDEBUG(StrReplace(
      GetLocStringByKey("rer_menu_preset_applied_message"),
      "{{preset_name}}",
      preset_name
    ));
  }

  public function tryApplyMenuPreset(master: CRandomEncounters) {
    this.done = false;
    this.master = master;

    this.GotoState('Waiting');
    var preset: RER_MenuPreset = StringToInt(RER_menu('RERmain', 'RERmenuPreset')) as RER_MenuPreset;

    NLOG("RER_PresetManager::tryApplyMenuPreset(), preset = " + preset);

     if (preset == RER_MenuPreset_Default) {
      this.GotoState('Vanilla');
      return;
    }

    if (preset == RER_MenuPreset_EnhancedEdition) {
      this.GotoState('EnhancedEdition');
      return;
    }

    this.done = true;
  }
}

enum RER_MenuPreset {
  RER_MenuPreset_Default = 0,
  RER_MenuPreset_None = 1,
  RER_MenuPreset_EnhancedEdition = 2,
}
