
state Waiting in CRandomEncounters {
  event OnEnterState(previous_state_name: name) {
    super.OnEnterState(previous_state_name);
    NLOG("Entering state WAITING");

    parent.ticks_before_spawn = this.calculateRandomTicksBeforeSpawn();

    if (previous_state_name == 'SpawningCancelled') {
      parent.ticks_before_spawn = parent.ticks_before_spawn / 3;
    }

    if (parent.rExtra.isPlayerInSettlement()) {
      parent.ticks_before_spawn = (parent.ticks_before_spawn * parent.settings.settlement_delay_multiplier) as int;
    }

    NLOG("waiting " + parent.ticks_before_spawn + " ticks");

    this.startWaiting();
  }

  entry function startWaiting() {
    var time_before_updating_frequency_multiplier: float;
    var ticks: float;

    parent.refreshEcosystemFrequencyMultiplier();
    parent.refreshModPower();

    time_before_updating_frequency_multiplier = 30;
    NLOG("ecosystem_frequency_multiplier = " + parent.ecosystem_frequency_multiplier);

    while (parent.ticks_before_spawn >= 0) {
      ticks = 5
            * parent.ecosystem_frequency_multiplier

            // The speed at which encounters are spawned scales with the mod power.
            * parent.getModPower();

      parent.ticks_before_spawn -= ticks;
      time_before_updating_frequency_multiplier -= ticks;

      // We refresh the ecosystem effects on frequencies every 30 seconds or so.
      if (time_before_updating_frequency_multiplier <= 0) {
        parent.refreshEcosystemFrequencyMultiplier();
        NLOG("ecosystem_frequency_multiplier = " + parent.ecosystem_frequency_multiplier);
      }

      Sleep(5);
    }

    parent.GotoState('Spawning');
  }

  function calculateRandomTicksBeforeSpawn(): int {
    if (theGame.envMgr.IsNight()) {
      return RandRange(parent.settings.customNightMin, parent.settings.customNightMax)
           + parent.settings.additional_delay_per_player_level * RER_getPlayerLevel();
    }

    return RandRange(parent.settings.customDayMin, parent.settings.customDayMax)
         + parent.settings.additional_delay_per_player_level * RER_getPlayerLevel();
  }
}
