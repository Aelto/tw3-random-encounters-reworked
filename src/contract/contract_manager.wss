

statemachine class RER_ContractManager {
  var master: CRandomEncounters;

  function init(_master: CRandomEncounters) {
    this.master = _master;

    this.GotoState('Waiting');
  }

  public function pickedContractNoticeFromNoticeboard(errand_name: string) {
    this.GotoState('DialogChoice');
  }

  //////////////////////////////////////////////////////////////////////////////
  // utility functions:

  public function getNearbyNoticeboard(): W3NoticeBoard {
    var entities: array<CGameplayEntity>;
    var board: W3NoticeBoard;
    var i: int;

    FindGameplayEntitiesInRange(
      entities,
      thePlayer,
      20, // range, 
      1, // max results
      , // tag: optional value
      FLAG_ExcludePlayer,
      , // optional value
      'W3NoticeBoard'
    );

    // bold move here, if there are no noticeboard nearby the game will crash.
    board = entities[0] as W3NoticeBoard;

    return board;
  }

  public function getMaximumContractCount(): int {
    return 2 + Min(this.getMaximumDifficulty() / 30, 8);
  }

  //////////////////////////////////////////////////////////////////////////////
  // contract workflow:

  public function startContract(factory: RER_ContractSeedFactory) {
    var contract: RER_Contract = (new RER_Contract in this).init(factory);

    this.master.storages.contract.has_ongoing_contract = true;
    this.master.storages.contract.active_contract = factory;

    this.master.storages.contract.killed_targets.Clear();
    var species: array<CreatureType> = contract.speciesList();
    for c: CreatureType in species {
      this.master.storages.contract.killed_targets.PushBack(false);
    }

    this.master.storages.contract.save();
    this.GotoState('Waiting');
  }

  public function hasOngoingContract(): bool {
    return this.master.storages.contract.has_ongoing_contract;
  }

  public function endOngoingContract() {
    this.master.storages.contract.has_ongoing_contract = false;
    this.master.storages.contract.save();
  }

  public function getOngoingContractFactory(): RER_ContractSeedFactory {
    return this.master.storages.contract.active_contract;
  }
  
  public function isTargetKilled(index: int): bool {
    if (index >= this.master.storages.contract.killed_targets.Size()) {
      return false;
    }

    return this.master.storages.contract.killed_targets[index];
  }

  public function setTargetKilled(index: int) {
    this.master.storages.contract.killed_targets[index] = true;
    this.master.storages.contract.save();
  }

  public function areAllTargetsKilled(): bool {
    for killed: bool in this.master.storages.contract.killed_targets {
      if (!killed) {
        return false;
      }
    }

    return true;
  }

  public function clearContractStorage() {
    this.master.storages.contract.completed_contracts.Clear();
    this.master.storages.contract.has_ongoing_contract = false;
    this.master.storages.contract.killed_targets.Clear();
    this.master.storages.contract.save();
  }

  public function completeOngoingContract() {
    if (!this.hasOngoingContract()) {
      return;
    }

    var contract: RER_Contract = (new RER_Contract in this)
      .init(this.getOngoingContractFactory());

    NLOG("completeOngoingContract(), seed = " + contract.seed);

    var has_bonus_rewards: bool = contract.hasBonusRewards();
    var total_crowns_amount: float = 0;
    var total_reputation_points: float = 0;
    var species_list: array<CreatureType> = contract.speciesList();
    for species: CreatureType in species_list {
      var bestiary_entry: RER_BestiaryEntry = this.master.bestiary
        .getEntry(this.master, species);

      if (has_bonus_rewards) {
        var enemy_count: int = bestiary_entry.getSpawnCount(this.master);

        var crowns_amount_settings: float = bestiary_entry.getCrowns(
          master.settings,
          EncounterType_CONTRACT,
          // every level increases the upper limit by 1%
          RandRangeF(
            1 + contract.factory.level * 0.01,
            0.8 + contract.factory.level * 0.005
          )
        );
          
        total_crowns_amount += enemy_count * crowns_amount_settings;
      }
      
      // the amount of reputation depends on the creatures and their ecosystem
      // impact.
      total_reputation_points += MaxF(
        bestiary_entry.ecosystem_delay_multiplier * 0.25,
        1
      );
    }

    // each additional group grants a 10% bonus
    total_crowns_amount *= 1 + (species_list.Size() * 0.1);

    if (contract.hasBonusReputation()) {
      total_reputation_points *= 1.5;
    }

    NLOG("completeOngoingContract(), total_crowns_amount = " + total_crowns_amount);
    NLOG("completeOngoingContract(), total_reputation_points = " + total_reputation_points);

    if (has_bonus_rewards) {
      thePlayer.AddMoney(total_crowns_amount as int);
      thePlayer.DisplayItemRewardNotification('Crowns', total_crowns_amount as int);
      theSound.SoundEvent("gui_inventory_buy");
    }

    RER_addContractReputationFactValue(RoundF(total_reputation_points));
    thePlayer.DisplayHudMessage(GetLocStringByKeyExt("rer_contract_finished"));

    this.endOngoingContract();
  }

  //////////////////////////////////////////////////////////////////////////////
  // difficulty logic:

  public function getMaximumDifficulty(): int {
    return RER_getContractReputationFactValue() * 1 + 1;
  }

  public function setPreferredDifficuty(difficulty: int) {
    // setting the value to either the max or the min resets it
    if (difficulty <= 0 || difficulty == this.getMaximumDifficulty()) {
      RER_removeContractPreferredDifficultyFact();
    }
    else {
      RER_setContractPreferredDifficultyFactValue(difficulty);
    }
  }

  private function getPreferredDifficulty(): int {
    var prefered: int = RER_getContractPreferredDifficultyFactValue();
    var difficulty: int = this.getMaximumDifficulty();

    if (prefered <= 0 || prefered > difficulty) {
      return difficulty;
    }

    return prefered;
  }

  public function getSelectedDifficulty(): int {
    var difficulty: int = this.getPreferredDifficulty();

    return difficulty;
  }

  public function contractHagglePreferredDifficultySelected(difficulty: int) {
    this.setPreferredDifficuty(difficulty);
    this.GotoState('DialogChoice');
  }
}
