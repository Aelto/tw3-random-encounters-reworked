
/**
 * In this state we display a list of dialogue options to the player so that
 * he can pick the contract he likes.
 */
state DialogChoice in RER_ContractManager {
  var camera: SU_StaticCamera;
  var completed: bool;

  event OnEnterState(previous_state_name: name) {
    super.OnEnterState(previous_state_name);
    NLOG("RER_ContractManager - state DialogChoice");

    this.DialogChoice_main();
  }

  private var menu_distance_value: float;

  entry function DialogChoice_main() {
    this.completed = false;
    this.startNoticeboardCutscene();

    while (!this.completed) {
      this.DialogChoice_prepareAndDisplayDialogChoices();
    }
  }

  private latent function startNoticeboardCutscene() {
    var noticeboard: W3NoticeBoard;

    RER_tutorialTryShowNoticeboard();

    REROL_mhm();
    Sleep(0.1);

    this.camera = SU_getStaticCamera();
    noticeboard = parent.getNearbyNoticeboard();
    
    this.camera.teleportAndLookAt(
      noticeboard.GetWorldPosition() + VecFromHeading(noticeboard.GetHeading()) * 2 + Vector(0, 0, 1.5),
      noticeboard.GetWorldPosition() + Vector(0, 0, 1.5)
    );

    theGame.FadeOut(0.2);
    this.camera.start();
    theGame.FadeInAsync(0.4);
  }

  private latent function DialogChoice_prepareAndDisplayDialogChoices() {
    var contracts_count: int = parent.getMaximumContractCount();

    var noticeboard: W3NoticeBoard = parent.getNearbyNoticeboard();
    var difficulty: int = parent.getSelectedDifficulty();
    var contracts: array<RER_Contract>;

    var timeoffset: int = GameTimeHours(theGame.CalculateTimePlayed())
      / RER_getHoursBeforeNewContract();

    var i: int;
    for (i = contracts_count; i > 0; i -= 1) {
      var factory: RER_ContractSeedFactory = RER_ContractSeedFactory(
        noticeboard.GetWorldPosition(),
        difficulty,
        i,
        SUH_getCurrentRegion(),
        timeoffset
      );

      var contract: RER_Contract = new RER_Contract in parent;
      contract.init(factory);

      contracts.PushBack(contract);
    }

    var choices: array<SSceneChoice>;

    choices.PushBack(SSceneChoice(
      StrReplace(
        GetLocStringByKey("rer_select_difficulty"),
        "{{difficulty}}",
        difficulty
      ),
      false,
      true,
      false,
      DialogAction_EXIT,
      'SelectDifficulty'
    ));

    var i: int;
    for (i = 0; i < contracts.Size(); i += 1) {
      var contract: RER_Contract = contracts[i];
      var species_list: array<CreatureType> = contract.speciesList();
      // the addition of the index here helps us differentiate the contracts
      // after the player has selected them, so we can do a strcmp and be sure
      // it's the one we want.
      var line: string = "[" + contract.difficulty() + "-" + (i + 1) + "] ";
      var require_comma: bool = false;

      for species: CreatureType in species_list {
        if (require_comma) {
          line += ", ";
        }
        require_comma = true;

        line += upperCaseFirstLetter(
          getCreatureNameFromCreatureType(
            parent.master.bestiary, species
          )
        );
      }

      var has_bonus_rewards: bool = contract.hasBonusRewards();
      if (has_bonus_rewards) {
        line += " (" + GetLocStringByKey('rer_contract_crowns_reward') + ")";
      }

      if (contract.hasBonusReputation()) {
        line += " (" + GetLocStringByKey('rer_contract_reputation_bonus') + ")";
      }

      choices.PushBack(SSceneChoice(
        upperCaseFirstLetter(line),
        has_bonus_rewards,
        false, // already choosen
        false,
        DialogAction_MONSTERCONTRACT,
        'StartContract'
      ));
    }

    choices.PushBack(SSceneChoice(
      GetLocStringByKey("rer_cancel"),
      false,
      true,
      false,
      DialogAction_EXIT,
      'Cancel'
    ));

    this.DialogChoice_displayDialogChoices(choices, contracts);
  }

  private latent function DialogChoice_displayDialogChoices(
    choices: array<SSceneChoice>,
    contracts: array<RER_Contract>
  ) {
    // while on gamepad, the interact input is directly sent in the dialog choice
    // it is safer to wait a bit before capturing the input.
    Sleep(0.25);

    var response: SSceneChoice = SU_setDialogChoicesAndWaitForResponse(choices);
    SU_closeDialogChoiceInterface();

    if (!IsNameValid(response.playGoChunk) || response.playGoChunk == 'Cancel') {
      this.camera.Stop();
      Sleep(0.25);
      parent.GotoState('Waiting');
      return;
    }

    if (response.playGoChunk == 'SelectDifficulty') {
      this.camera.Stop();

      var haggle: RER_ContractModuleDialog = new RER_ContractModuleDialog in this;
      haggle.openDifficultySelectorWindow(parent);

      parent.GotoState('Waiting');

      return;
    }

    var offset: int = 1;

    NLOG("DialogChoice_displayDialogChoices" + response.description);

    var i: int;
    for (i = 0; i < choices.Size(); i += 1) {
      if (StrContains(response.description, choices[i].description)) {
        // there is the select difficulty first, make sure to add the -1 offset
        var selected_contract: RER_Contract = contracts[i - 1];

        if (selected_contract) {
          this.completed = true;
          theSound.SoundEvent("gui_ingame_quest_active");
          this.camera.Stop();

          NHUD(GetLocStringByKey('rer_contract_started'));
          // this.openMapMenuForContract(selected_contract);

          parent.startContract(selected_contract.factory);          
          return;
        }
      }
    }

    NDEBUG("RER ERROR: Unable to get creature_type from dialogue choices");
  }

  private function openMapMenuForContract(contract: RER_Contract) {
    var initData : W3MapInitData;
		
		initData = new W3MapInitData in this;
		initData.ignoreSaveSystem = true;
		initData.setDefaultState('FastTravel');
		theGame.RequestMenuWithBackground( 'MapMenu', 'CommonMenu', initData );
  }
}